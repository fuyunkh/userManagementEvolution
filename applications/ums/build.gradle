buildscript {
    ext {
        springBootVersion = '1.3.0.M5'
    }
    repositories {
        mavenCentral()
        maven {
            url 'http://repo.spring.io/milestone'
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:0.5.2.RELEASE")
        classpath 'au.com.dius:pact-jvm-provider-gradle_2.10:2.2.1'
    }
}

apply plugin: 'spring-boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'au.com.dius.pact'

jar {
    baseName = 'ums'
    version = '0.0.1-SNAPSHOT'
}

repositories {
    mavenCentral()
    maven {
        url 'http://repo.spring.io/milestone'
    }
    maven {
        url 'http://repo.spring.io/libs-snapshot'
    }
}

dependencyManagement {
    imports {
        mavenBom "io.pivotal.spring.cloud:spring-cloud-services-starter-parent:1.0.0.BUILD-SNAPSHOT"
    }
}

// Fix for Feign clients NullPointerException with 1.0 version of Spring Cloud Services
// You are forcing a newer version of Spring Cloud Nextflix to get the patch
// because the SCS starter parent needs to depend on 1.0.3.RELEASE for now.
ext["spring-cloud-netflix.version"] = "1.0.4.RELEASE"

dependencies {
    compile(project(":components/subscriptions"))
    compile(project(":components/email"))
    compile(project(":components/users"))
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-jdbc")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("io.pivotal.spring.cloud:spring-cloud-services-starter-service-registry")
    compile("io.pivotal.spring.cloud:spring-cloud-services-starter-config-client")
    compile("com.h2database:h2")
}

// SUBSCRIBER PROVIDER PACT TEST
pact {
    serviceProviders {
        // You can define as many as you need, but each must have a unique name
        provider1 {
            // All the provider properties are optional, and have sensible defaults (shown below)
            protocol = 'http'
            host = 'localhost'
            port = 8082
            path = '/'

            // Again, you can define as many consumers for each provider as you need, but each must have a unique name
            hasPactWith('javascriptclient') {

                // currently supports a file path using file() or a URL using url()
                pactFile = file('../../pact-js-consumer-example/pacts/javascriptclient-ums.json')


            }
        }
    }
}
